Import('env')

import sys
import os.path

if sys.platform == 'win32':
    THRIFT = "J:/sources/thrift/"
    LIBEVENT = "J:/sources/libevent/"
else:
    THRIFT = "/usr/local/thrift-0.6.1/"
    LIBEVENT = "/usr/local/libevent-2.0.12/"

SRC_ROOT = env['SRC_ROOT']
BOOST_ROOT = env['BOOST_ROOT']
JPEG_ROOT = SRC_ROOT + '../lib/libjpeg/'

gensrc = [
     SRC_ROOT + 'gen-cpp/imaging_constants.cpp',
     SRC_ROOT + 'gen-cpp/imaging_types.cpp',
     SRC_ROOT + 'gen-cpp/Imaging.cpp',
]

serversrc = [
	SRC_ROOT + 'CppServer.cpp',
    SRC_ROOT + 'ImagingHandler.cpp',
    SRC_ROOT + 'ImagingAsyncHandler.cpp',
    SRC_ROOT + 'ImagingThriftUtils.cpp',
]

clientsrc = [
	SRC_ROOT + 'CppClient.cpp',
]

cpppath = [
    BOOST_ROOT + 'boost/tr1',
    BOOST_ROOT,
    SRC_ROOT + "gen-cpp",
    LIBEVENT + 'include',
    JPEG_ROOT,
]

cppdefines = {
     'BOOST_ALL_NO_LIB':None,
     'HAVE_CONFIG_H':None,
}

if sys.platform == 'win32':
    pthreadVC2 = "pthreadVC2" if env['OS32'] else "pthreadVC2_x64"
    libpath = [
        LIBEVENT + 'build/${BUILD_PLATFORM}/%s${BUILD_TARGET}/' % ("" if env['OS32'] else "x64/"),
        THRIFT + 'build/${BUILD_PLATFORM}/%s${BUILD_TARGET}/' % ("" if env['OS32'] else "x64/"),
        THRIFT + 'build/${BUILD_PLATFORM}/%s/' % pthreadVC2,
    ]
    libs = [
        'libevent',
        'thriftnb',
        'pthreadVC2' if env['OS32'] else 'pthreadVC2_x64',
        'Ws2_32',
    ]
    cpppath += [
        LIBEVENT + 'WIN32-Code',
        LIBEVENT + 'compat',
        LIBEVENT,
        THRIFT + 'lib/cpp/src',
        THRIFT + 'build/vc10',
    ]
    cppdefines.update({
        "_SCL_SECURE_NO_WARNINGS" : "1",
        "_SECURE_SCL" : "0",
    })
else:
    libpath = [
        THRIFT + 'lib',
        LIBEVENT + 'lib',
    ]
    libs = [
        'thrift',
        'thriftnb',
        'event',
    ]
    cpppath += [
        THRIFT + 'include/thrift',
    ]


clientdeps = [
    'myjpeg',
    'boost_filesystem',
]


serverdeps = [
    'myjpeg',
    'boost_filesystem',
    'boost_programoptions',
]


if sys.platform == 'win32':
    pthread = THRIFT + 'build/${BUILD_PLATFORM}/%s/%s.dll' % (pthreadVC2, pthreadVC2),
    cmd = env.Command("$BUILD_DIR/%s.dll" % pthreadVC2, env.NoCache(pthread), env.AbbCopy('$TARGET', '$SOURCE'))
    copy_pthread = env.Alias('copy_pthread', cmd)


client = env.AbbProg(progname="client", sources=gensrc + clientsrc,
                        defines=cppdefines, includes=cpppath,
                        libpath=libpath, libs=libs,
                        deps=clientdeps)
server = env.AbbProg(progname="server", sources=gensrc + serversrc,
                        defines=cppdefines, includes=cpppath,
                        libpath=libpath, libs=libs,
                        deps=serverdeps)

target = Alias('apps', [client, server])

if sys.platform == 'win32':
    env.Requires(client, copy_pthread)
    env.Requires(server, copy_pthread)

Return("target")
